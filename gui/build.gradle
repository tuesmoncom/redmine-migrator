apply plugin: 'groovy'
apply plugin: 'com.github.johnrengelman.shadow'

apply plugin: 'org.asciidoctor.gradle.asciidoctor'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.3'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.0'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://viewaframework.sourceforge.net/m2-repo'
    }
    maven {
        url 'http://dl.bintray.com/kaleidos/maven'
    }
    maven {
        url 'https://github.com/eugener/oxbow/raw/master/maven/repository'
    }
    maven {
        url 'http://dl.bintray.com/marioggar/maven'
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {

    // FNZ
    compile('fnz:fnz-jdk7:1.0.5') {
        exclude module: 'log4j'
        exclude module: 'groovy'
        exclude module: 'groovy-all'
    }
    // GUI DEPENDENCIES
    compile 'org.viewaframework:viewaframework-core:1.0.9'
    compile 'org.viewaframework:viewaframework-widget:1.0.9'
    compile('org.viewaframework:viewaframework-docking:1.0.9') {
        exclude group: 'org.dockingframes'
    }

    compile 'com.jgoodies:jgoodies-looks:2.5.2'
    compile 'org.ini4j:ini4j:0.5.2'

    // TUESMON MIGRATOR
    compile project(':redmine')

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'junit:junit:4.11'
}

jar {
    manifest {
        attributes(
            "Main-Class": "gui.TuesmonApp",
            "Implementation-Title": "Tuesmon",
            "Implementation-Version": '1.0.0-SNAPSHOT',
            "SplashScreen-Image": 'gui/img/tuesmon_about.png'
        )
    }
}

task migratorUI(type: JavaExec) {
    group = 'tuesmon'
    description = 'Runs migrator GUI'
    main = 'gui.TuesmonApp'
    classpath = sourceSets.main.runtimeClasspath
}

asciidoctor {
    sourceDir = 'src/docs' as File
    options = [
        attributes: [
            'icons': 'font',
            'source-highlighter': 'prettify',
            'toc': 'left',
            'toclevels': 4
        ]
    ]
}